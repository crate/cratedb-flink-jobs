buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.1'
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'com.github.johnrengelman.shadow'

repositories {
    jcenter()
}

group = 'io.crate'
archivesBaseName = 'cratedb-flink-jobs'
version = 0.2

ext {
    scalaVersion = '2.12'
    flinkVersion = '1.12.2'
    jacksonVersion = '2.12.3'
    crateJDBCVersion = '2.6.0'
    postgresJDBCVersion = '42.2.20'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
    // Exclude following dependencies from our shadowJar
    runtime.exclude module: "flink-java"
    runtime.exclude module: "flink-streaming-java_${scalaVersion}"
    runtime.exclude module: "flink-clients_${scalaVersion}"
}

dependencies {
    // Flink
    // https://mvnrepository.com/artifact/org.apache.flink
    compile "org.apache.flink:flink-java:${flinkVersion}"
    compile "org.apache.flink:flink-streaming-java_${scalaVersion}:${flinkVersion}"
    compile "org.apache.flink:flink-connector-jdbc_${scalaVersion}:${flinkVersion}"

    // Kafka
    // https://mvnrepository.com/artifact/org.apache.flink
    // https://mvnrepository.com/artifact/com.fasterxml.jackson.datatype
    compile "org.apache.flink:flink-connector-kafka_${scalaVersion}:${flinkVersion}"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonVersion}"

    // CrateDB
    // https://mvnrepository.com/artifact/io.crate
    // https://mvnrepository.com/artifact/org.postgresql
    compile "io.crate:crate-jdbc:${crateJDBCVersion}"
    // TODO: Currently raises `org.postgresql.util.PSQLException: No hstore extension installed.`.
    //       compile "org.postgresql:postgresql:${postgresJDBCVersion}"

}

shadowJar {
    classifier = null
    manifest {
        attributes 'Main-Class': 'io.crate.streaming.TaxiRidesStreamingJob'
    }
}

assemble.dependsOn(shadowJar)

task wrapper(type: Wrapper) {
    gradleVersion = '4.10.2'
}
